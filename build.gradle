buildscript {
    ext.kotlin_version = "1.3.0"
    repositories {
        jcenter()
        maven { url = "https://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
//Only edit below this line, the above code adds and enables the necessary things for Forge to be setup.
apply plugin: 'kotlin'

version = "1.2.0"
group = "com.anatawa12.fixRtm"
archivesBaseName = "fixRtm"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2793"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not always work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "snapshot_20171003"
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

configurations{
    shade
    compile.extendsFrom shade
}

dependencies {
    // you may put jars on which you depend on in ./libs
    // or you may define them like so..
    //compile "some.group:artifact:version:classifier"
    //compile "some.group:artifact:version"
      
    // real examples
    //compile 'com.mod-buildcraft:buildcraft:6.0.8:dev'  // adds buildcraft to the dev env
    //compile 'com.googlecode.efficient-java-matrix-library:ejml:0.24' // adds ejml to the dev env

    // the 'provided' configuration is for optional dependencies that exist at compile-time but might not at runtime.
    //provided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // the deobf configurations:  'deobfCompile' and 'deobfProvided' are the same as the normal compile and provided,
    // except that these dependencies get remapped to your current MCP mappings
    //deobfCompile 'com.mod-buildcraft:buildcraft:6.0.8:dev'
    //deobfProvided 'com.mod-buildcraft:buildcraft:6.0.8:dev'

    // for more info...
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html

    shade "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    shade group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: '1.1.0'

    compileOnly files(new File(projectDir, "mods/rtm.jar"),
            new File(projectDir, "mods/ngtlib.jar"))
}

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else except the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

runClient {
    environment("fml.coreMods.load", "com.anatawa12.fixRtm.asm.FixRtmCorePlugin")
    systemProperties["legacy.debugClassLoading"] = "true"
    systemProperties["legacy.debugClassLoadingSave"] = "true"
    if (!hasProperty("noLogin") && hasProperty("minecraft.login.username") && hasProperty("minecraft.login.password"))
    args += ["-username", getProperty("minecraft.login.username"), "-password", getProperty("minecraft.login.password")]
}

jar {
    configurations.shade.each {dep ->
        from(project.zipTree(dep)){
            exclude 'META-INF', 'META-INF/**'
        }
    }

    manifest {
        attributes("FMLCorePlugin" : "com.anatawa12.fixRtm.asm.FixRtmCorePlugin",
        "FMLCorePluginContainsFMLMod": "*")
    }
}

task CopyJarToTest(type: Copy, dependsOn: reobfJar) {
    from jar.archivePath
    into new File(projectDir, "../fixRtmTest/mods")
}
//build.dependsOn += CopyJarToTest
compileKotlin {
    kotlinOptions {
        //freeCompilerArgs = ["-XXLanguage:+InlineClasses"]
    }
}

task downloadRTM (type: DownloadModJar) {
    projectId = 288988
    to = new File(projectDir, "mods/rtm.jar")
}

task downloadNgtLib (type: DownloadModJar) {
    projectId = 288989
    to = new File(projectDir, "mods/ngtlib.jar")
}

import java.nio.file.Files

task linkToModsDir() {
    def modsDir = new File(projectDir, "run/mods")

    dependsOn downloadRTM, downloadNgtLib

    def rtmInMods = new File(modsDir, downloadRTM.to.name)
    def ngtLibInMods = new File(modsDir, downloadNgtLib.to.name)

    outputs.files(rtmInMods, ngtLibInMods)

    doLast {
        rtmInMods.delete()
        ngtLibInMods.delete()
        Files.createSymbolicLink(rtmInMods.toPath(), downloadRTM.to.toPath())
        Files.createSymbolicLink(ngtLibInMods.toPath(), downloadNgtLib.to.toPath())
    }
}
